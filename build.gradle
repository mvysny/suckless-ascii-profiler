plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.31"
    id "com.jfrog.bintray" version "1.7.3"
}
defaultTasks "clean", "build"

group 'com.github.mvysny.sucklessprofiler'
version '0.4'

ext.local = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
}

apply plugin: 'application'
mainClassName = "com.github.mvysny.sucklessprofiler.SucklessProfilerKt"

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/mvysny/github" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.31"
    testCompile("com.github.mvysny.dynatest:dynatest:0.8")
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'suckless-profiler'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An embedded profiler which you start and stop on will, and it will dump the profiling info into the console')
                root.appendNode('name', 'Suckless ASCII Profiler')
                root.appendNode('url', 'https://github.com/mvysny/suckless-ascii-profiler')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'github'
        name = 'com.github.mvysny.sucklessprofiler'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/suckless-ascii-profiler'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        // to see the exceptions of failed tests in Travis-CI console. Seems to be ignored:
        // https://github.com/gradle/gradle/issues/4681
        exceptionFormat = 'full'
    }
}
